buildscript {
        repositories {
            google()
            mavenCentral()

            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }

        dependencies {
            classpath('com.android.tools.build:gradle:4.2.2')
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
            classpath 'org.jlleitschuh.gradle:ktlint-gradle:11.0.0'
        }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
//apply plugin: "org.jlleitschuh.gradle.ktlint"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 31)

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 24)
        targetSdkVersion safeExtGet('targetSdkVersion', 31)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

repositories {
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
    mavenCentral()
    google()
}

dependencies {
    implementation 'com.facebook.react:react-native:+'  // From node_modules
    implementation "androidx.core:core-ktx:1.3.2"

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.12'
    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:1.10.19'
    // Test final classes
    testImplementation 'org.mockito:mockito-inline:2.13.0'
    // helpers for kotlin
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.1.0"
    // mock Android stuff
    testImplementation "io.mockk:mockk:1.10.6"

    // Onegini SDK
    api('com.onegini.mobile.sdk.android:onegini-sdk:11.7.0@aar') {
        transitive = true
    }
}

allprojects {
    ext.artifactoryUser = project.hasProperty('artifactory_user') ? artifactory_user : System.getenv("ARTIFACTORY_USER")
    ext.artifactoryPassword = project.hasProperty('artifactory_password') ? artifactory_password : System.getenv("ARTIFACTORY_PASSWORD")
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        google()
        maven { url 'https://www.jitpack.io' }
         if (artifactoryUser && artifactoryPassword) {
            maven {
                url "https://repo.onegini.com/artifactory/onegini-sdk"
                credentials {
                    username artifactoryUser
                    password artifactoryPassword
                }
            }
        } else {
            throw new InvalidUserDataException("You must configure the 'artifactory_user' and 'artifactory_password' properties in your project before you can " +
                    "build it.")
        }
    }
}
